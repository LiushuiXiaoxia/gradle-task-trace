import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'maven-publish'
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/public/' }
    google()
    mavenCentral()
}

def BASE_VERSION = "0.0.1"
def version_build_gradle = "7.2.2"
def version_android_tool = "30.0.3"
def snapshot = System.getenv("SNAPSHOT")
logger.quiet("snapshot = $snapshot")

def vvv = BASE_VERSION
if (snapshot == "true") {
    vvv = "$BASE_VERSION-${new SimpleDateFormat("yyMMdd.HHmm").format(new Date())}"
}

group "cn.mycommons"
version vvv

sourceCompatibility = "8"
targetCompatibility = "8"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation gradleApi()
    // implementation localGroovy()

    def close = {
        exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
    }
    implementation("com.squareup.okhttp3:okhttp:4.9.1", close)
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.1", close)
    implementation "com.google.code.gson:gson:2.9.0"

    // implementation 'org.codehaus.groovy:groovy-all:2.5.12'
    compileOnly 'com.google.guava:guava:31.0.1-jre'

    testImplementation 'org.testng:testng:7.4.0'
    testImplementation "io.mockk:mockk:1.11.0"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useTestNG()
}

gradlePlugin {
    plugins {
        xbuild {
            id = 'xtrace'
            implementationClass = 'cn.mycommons.gtask.xtrace.XTracePlugin'
        }
    }
}

publishing {
    repositories {
        maven {
            url = 'xxx'
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
    publications {
        maven(MavenPublication) {
        }
    }
}

gradle.buildFinished {
    logger.quiet("version = $version")
}